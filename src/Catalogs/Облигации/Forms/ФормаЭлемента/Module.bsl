
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Элементы.SECID.ТолькоПросмотр = НЕ Объект.Ссылка.Пустая(); 
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьС_MOEX(Команда)
	ЗагрузитьС_MOEXНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьС_MOEXНаСервере()
//			"RU000A100WA8", 
	URL = СтрШаблон(
			"%1/%2.json?%3",
			"https://iss.moex.com/iss/securities",
			Объект.SECID,
			"iss.meta=off&iss.only=description&iss.json=extended"
		);

	ДП = КлиентHTTPКлиентСервер.НовыеДополнительныеПараметры();
	Ответ = КлиентHTTPКлиентСервер.ТелоОтветаКакJSON(ДП).Получить(URL, , ДП);
	
	Если Ответ.КодСостояния <> 200 Тогда
		ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						        	НСтр("ru='Ошибка получения данных с биржи. Код ответа <%1>'"),
						        	Ответ.КодСостояния);
		ОбщегоНазначения.СообщитьПользователю(ТекстПредупреждения);		
		Возврат;		
	КонецЕсли;
	
	Если Ответ.Тело[1].description.Количество() = 0 Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru='Ошибка получения данных с биржи. Тикер не найден'"));		
		Возврат;		
	КонецЕсли;
	
	Для Каждого эл Из Ответ.Тело[1].description Цикл
		Если эл.type = "date" Тогда
			Объект[эл.name] = ПрочитатьДатуJSON(эл.value, ФорматДатыJSON.ISO);
		ИначеЕсли эл.type = "boolean" Тогда
			Объект[эл.name] = ?(эл.value = "1", Истина, Ложь);		
		Иначе
			Объект[эл.name] = эл.value;
		КонецЕсли;		
	КонецЦикла;
	Объект.Наименование = Объект.NAME;
КонецПроцедуры



#КонецОбласти
